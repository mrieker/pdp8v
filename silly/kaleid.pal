
/ ../asm/assemble -pal kaleid.pal kaleid.obj > kaleid.lis
/ ../asm/link -o kaleid.oct kaleid.obj > kaleid.map

/       tubes: vc8pms=2000 vc8type=e switchregister=3 ../driver/raspictl kaleid.oct
/        zynq: vc8pms=2000 vc8type=e switchregister=3 ../driver/raspictl -zynqlib kaleid.oct
/  pidp panel: vc8pms=500 vc8type=e ../driver/raspictl -pidp -nohwlib kaleid.oct
/              ...switches set to 4003 for example

/ https://homepage.cs.uiowa.edu/~jones/pdp8/man/vc8e.html

/ Kaleidoscope program for the VC8E/VR14 scope.
/ Based on suggestions from page 6-19 of the Small Computer
/ Handbook, 1973.

/ The advice given is to experiment with settings of
/ the switch register bit positions 9, 10 and 11, leaving
/ all higher bits zero.  The advice is good; this is a
/ nice "screen saver" for non-storage scopes.

DISD=   6052
DILX=   6053
DILY=   6054
DIXY=   6055
DILE=	6056
DIRE=	6057

*	0020

X,      3777            / any initial value works, but
Y,      6               / 0,0 is least interesting.
INCS,	0
EXTRA,	0
BIAS,	0

TEM,    .-.
C,      .-.
THREE,	3
FOUR,	4
TWO,	2
O77,	077

*       0200
	.global	__boot
__boot,
START,
	ISZ	EXTRA	/ COUNT 4096 CYCLES
	JMP	SAMEXTRA
	TAD	BIAS	/ STEP BIAS
	TAD	THREE
	DCA	BIAS
	TAD	BIAS	/ SHORTEN THE 4096 CYCLES FOR NEXT TIME
	BSW
	DCA	EXTRA
	TAD	BIAS	/ DISPLAY SHIFT COUNT IN 'SC' LEDS ON PIDP PANEL
	RTR
	AND	THREE
	TAD	TWO
	CMA
	DCA	.+2
	SCL
SCOUNT,	7775		/ (one's comp shift count)
	DIRE		/ flip color
	TAD	FOUR
	AND	FOUR
	DILE
	CLA
SAMEXTRA,
	TAD	EXTRA	/ DISPLAY 4K COUNTER IN 'MQ' LEDS ON PIDP PANEL
	MQL

	TAD     Y
	JMS     SCALE
	CMA
	TAD     X
	DCA     X       / X = X - SCALE(Y)
	TAD     X
	JMS     SCALE
	TAD     Y
	DCA     Y       / Y = Y - SCALE(X)
	JMS	PLOTXY
	OSR
	SMA CLA
	JMP	START
	IAC
	JMS	PLOTXY
	CLL IAC RAL
	JMS	PLOTXY
	CLL CML IAC RAL
	JMS	PLOTXY
	JMP	START

PLOTXY,	.-.
	DCA	INCS
	TAD	INCS
	CLL RAR
	DIXY		/ previous ppint should be ready to plot by now
	TAD	X	/ send new point X to display
	DILX
	CLA IAC
	AND	INCS
	TAD	Y	/ send new point Y to display
	DILY
	CLL CLA
	JMP I	PLOTXY

/ Divide AC (signed) by 2**(BIAS/4&3+2)
SCALE,  .-.
	DCA     TEM
	TAD	SCOUNT	/ one's comp shift count
	IAC		/ two's comp shift count
	DCA     C
	TAD     TEM	/ get signed AC value
	SPA
	JMP	SCNEG
SCPOS,
	CLL
	RAR             / Arithmetic right shift
	ISZ     C
	JMP     SCPOS
	JMP I   SCALE
SCNEG,
	CLL CML
	RAR             / Arithmetic right shift
	ISZ     C
	JMP     SCNEG
	JMP I   SCALE

